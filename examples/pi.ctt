-- Characterization of equality in pi types.
module pi where

import equiv

              -----------------------------------
              -- Example: Equality in pi types --
              -----------------------------------

pi (A:U) (P:A->U) : U = (x:A) -> P x

idPi (A:U) (B:A->U) (f g : pi A B) : Path U (Path (pi A B) f g) ((x:A) -> Path (B x) (f x) (g x)) =
 isoPath  (Path (pi A B) f g) ((x:A) -> Path (B x) (f x) (g x)) F G S T
 where T0 : U = Path (pi A B) f g
       T1 : U = (x:A) -> Path (B x) (f x) (g x)
       F (p:T0) : T1 = \ (x:A) -> <i>p@i x
       G (p:T1) : T0 = <i>\ (x:A) -> p x @ i
       S (p:T1) : Path T1 (F (G p)) p = refl T1 p
       T (p:T0) : Path T0 (G (F p)) p = refl T0 p

setPi (A:U) (B:A -> U) (h:(x:A) -> set (B x)) (f g:pi A B) : prop (Path (pi A B) f g) =
 rem
 where
  T : U = (x:A) -> Path (B x) (f x) (g x)
  rem1 : prop T = \ (p q : T) -> <i> \ (x:A) -> h x (f x) (g x) (p x) (q x)@i

  rem : prop (Path (pi A B) f g) =
    subst U prop T (Path (pi A B) f g) (<i>idPi A B f g@-i) rem1

groupoidPi (A:U) (B:A -> U) (h:(x:A) -> groupoid (B x)) (f g:pi A B) : set (Path (pi A B) f g) =
    subst U set  T  (Path (pi A B) f g) (<i>idPi A B f g@-i) rem1
 where
  T : U = (x:A) -> Path (B x) (f x) (g x)
  rem1 : set T =  setPi A (\ (x:A) -> Path (B x) (f x) (g x)) (\ (x:A) -> h x (f x) (g x))


propPi2 (A : U) (B0 : A -> A -> U) (h0 : (x y : A) -> prop (B0 x y))
  : prop ((x y : A) -> B0 x y)
  = let
    p0 (a : A) : prop ((b : A) -> B0 a b)
      = propPi A (B0 a) (h0 a)
    B1 (a : A) : U
      = (b : A) -> B0 a b
  in propPi A B1 p0

propPi3 (A : U) (B0 : A -> A -> A -> U) (h0 : (x y z : A) -> prop (B0 x y z))
  : prop ((x y z : A) -> B0 x y z)
  = let
    p0 (a b : A) : prop ((c : A) -> B0 a b c)
      = propPi A (B0 a b) (h0 a b)
    B1 (a b : A) : U
      = (c : A) -> B0 a b c
  in propPi2 A B1 p0

