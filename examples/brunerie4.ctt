-- Version of brunerie3 with some undefined just to track down which
-- definitions are used in the computation.
module brunerie4 where

Path (A : U) (a0 a1 : A) : U = PathP (<i> A) a0 a1

compPath (A : U) (a b c : A) (p : Path A a b) (q : Path A b c) : Path A a c =
  <i> hcomp A (p @ i) [ (i =0) -> <j> a, (i = 1) -> q ]

data bool = false | true
data nat = zero | suc (n : nat)

idfun (A : U) (a : A) : A = a

pred : nat -> nat = split
  zero -> zero
  suc n -> n

{- Z is represented as:

  +2 = pos (suc (suc zero))
  +1 = pos (suc zero)
   0 = pos zero
  -1 = neg zero
  -2 = neg (suc zero)

-}
data Z = pos (n : nat) | neg (n : nat)

twoZ : Z = pos (suc (suc zero))
oneZ : Z = pos (suc zero)
zeroZ : Z = pos zero
moneZ : Z = neg zero
mtwoZ : Z = neg (suc zero)

data S1 = base1
        | loop1 <i> [ (i=0) -> base1
                    , (i=1) -> base1 ]

data S2 = base2
        | loop2 <i j> [ (i=0) -> base2
                      , (i=1) -> base2
                      , (j=0) -> base2
                      , (j=1) -> base2]

data S3 = base3
        | loop3 <i j k> [ (i=0) -> base3
                        , (i=1) -> base3
                        , (j=0) -> base3
                        , (j=1) -> base3
                        , (k=0) -> base3
                        , (k=1) -> base3]

loopS1 : U = Path S1 base1 base1
loop : loopS1 = <i> loop1{S1} @ i

-- Pointed types
ptType : U = (A : U) * A
pt (A : ptType) : A.1 = A.2

boolpt : ptType = (bool,true)
S1pt : ptType = (S1,base1)
S2pt : ptType = (S2,base2)
S3pt : ptType = (S3,base3)

ptMap (A B : ptType) : U = (f : A.1 -> B.1) * (Path B.1 (f (pt A)) (pt B))

-- The first 3 loop spaces of a pointed type.
-- TODO: Maybe defined these by induction on n as in experiments/pointed.ctt?
Omega (A : ptType) : ptType = (Path A.1 (pt A) (pt A),<_> pt A)
Omega2 (A : ptType) : ptType = Omega (Omega A)
Omega3 (A : ptType) : ptType = Omega2 (Omega A)

-- Simplified mapOmega when the function is pointed by reflexivity
mapOmegaRefl (A : ptType) (B : U) (h : A.1 -> B) (p : (Omega A).1) :
  (Omega (B, h (pt A))).1 = <i> h (p @ i)

mapOmegaRefl2 (A : ptType) (B : U) (h : A.1 -> B) (p : (Omega2 A).1) :
  (Omega2 (B, h (pt A))).1 = <i j> h (p @ i @ j)
  -- mapOmegaRefl (Omega A) (Omega (B,h (pt A))).1 (mapOmegaRefl A B h) p

mapOmegaRefl3 (A : ptType) (B : U) (h : A.1 -> B) (p : (Omega3 A).1) :
  (Omega3 (B, h (pt A))).1 = <i j k> h (p @ i @ j @ k)
  -- mapOmegaRefl2 (Omega A) (Omega (B,h (pt A))).1 (mapOmegaRefl A B h)


data join (A B : U) = inl (a : A)
                    | inr (b : B)
                    | push (a : A) (b : B) <i> [ (i = 0) -> inl a
                                               , (i = 1) -> inr b ]

pushP (A B : U) (a : A) (b : B) : Path (join A B) (inl a) (inr b) =
  <i> push {join A B} a b @ i

joinpt (A : ptType) (B : U) : ptType = (join A.1 B,inl (pt A))

Square (A : U) (a0 a1 b0 b1 : A)
               (u : Path A a0 a1) (v : Path A b0 b1)
               (r0 : Path A a0 b0) (r1 : Path A a1 b1) : U
  = PathP (<i> (Path A (u @ i) (v @ i))) r0 r1

-- Inspired by Evan's redtt code
goalcube'' : Path (Path (Path (join S1 S1)
                            (inl base1) (inl base1))
                      (<_> inl base1) (<_> inl base1))
                (<_ _> inl base1) (<_ _> inl base1) =
  <i j k> hcomp (join S1 S1)
                (pushP S1 S1 (loop @ j) (loop @ k) @ i)
                [
                  (i=0) -> facek01 @ 1 @ j
                , (i=1) -> <m> pushP S1 S1 base1 (loop @ k) @ -m
                , (j=0) -> <m> pushP S1 S1 base1 (loop @ k) @ i /\ -m
                , (j=1) -> <m> pushP S1 S1 base1 (loop @ k) @ i /\ -m
                , (k=0) -> facek01 @ -i @ j
                , (k=1) -> facek01 @ -i @ j
                ]

  where
  facek01 : PathP (<i> PathP (<j> Path (join S1 S1)
                                       (pushP S1 S1 (loop @ j) base1 @ -i)
                                       (inl base1))
                             (<k> pushP S1 S1 base1 base1 @ -i /\ -k)
                             (<k> pushP S1 S1 base1 base1 @ -i /\ -k))
                  (<j k> pushP S1 S1 base1 base1 @ -k)
                  (<j k> hcomp (join S1 S1)
                               (pushP S1 S1 base1 base1 @ -k)
                               [ (j=0) -> <i> pushP S1 S1 base1 base1 @ -i /\ -k
                               , (j=1) -> <i> pushP S1 S1 base1 base1 @ -i /\ -k
                               , (k=0) -> <i> pushP S1 S1 (loop @ j) base1 @ -i
                               , (k=1) -> <_> inl base1
                               ]) =
   <i j k> hfill (join S1 S1)
                 (pushP S1 S1 base1 base1 @ -k)
                 [ (j=0) -> <i> pushP S1 S1 base1 base1 @ -i /\ -k
                 , (j=1) -> <i> pushP S1 S1 base1 base1 @ -i /\ -k
                 , (k=0) -> <i> pushP S1 S1 (loop @ j) base1 @ -i
                 , (k=1) -> <_> inl base1
                 ] @ i

e : S3 -> join S1 S1 = split
  base3 -> inl base1
  loop3 @ i j k -> goalcube'' @ i @ j @ k

eInvAux2 : S1 -> Path (Path S3 base3 base3) (<_> base3) (<_> base3) = split
  base1 -> <_ _> base3
  loop1 @ i -> <j k> loop3{S3} @ i @ j @ k

eInvAux (x : S1) : S1 -> Path S3 base3 base3 = split
  base1 -> <_> base3
  loop1 @ i -> eInvAux2 x @ i

eInv : join S1 S1 -> S3 = split
  inl x -> base3
  inr y -> base3
  push x y @ i -> eInvAux x y @ i

merid : S1 -> Path S2 base2 base2 = split
  base1 -> <_> base2
  loop1 @ i -> <j> loop2{S2} @ i @ j

foo (x y : S1) : Path S2 base2 base2 =
  compPath S2 base2 base2 base2 (merid y) (merid x)

alpha : join S1 S1 -> S2 = split
  inl x -> base2
  inr y -> base2
  push x y @ i -> foo x y @ i

-- alphaS3 : S3 -> S2 = split
--   base3 -> base2
--   loop3 @ i j k ->
--     hcomp S2 (loop2{S2} @ i @ k)
--       [ (i=0) -> <l> loop2{S2} @ j @ k /\ - l
--       , (i=1) -> <l> loop2{S2} @ j @ k /\ - l
--       , (k=0) -> <_> base2
--       , (k=1) -> <l> loop2{S2} @ j @ l
--       , (j=0) -> <l> loop2{S2} @ i @ k
--       , (j=1) -> <l> loop2{S2} @ i @ k ]

--
Sigma (A : U) (B : A -> U) : U = (x : A) * B x
refl (A : U) (a : A) : Path A a a = <i> a

constSquare (A : U) (a : A) (p : Path A a a) : Square A a a a a p p p p =
  <i j> hcomp A a [ (i = 0) -> <k> p @ (j \/ - k)
                  , (i = 1) -> <k> p @ (j /\ k)
                  , (j = 0) -> <k> p @ (i \/ - k)
                  , (j = 1) -> <k> p @ (i /\ k)]

fiber (A B : U) (f : A -> B) (y : B) : U =
  (x : A) * Path B y (f x)
isContr (A : U) : U = (x : A) * ((y : A) -> Path A x y)
prop (A : U) : U = (a b : A) -> Path A a b
set (A : U) : U = (a b : A) -> prop (Path A a b)
groupoid (A : U) : U = (a b : A) -> set (Path A a b)
twogroupoid (A : U) : U = (a b : A) -> groupoid (Path A a b)
threegroupoid (A : U) : U = (a b : A) -> twogroupoid (Path A a b)

SET : U = (A : U) * set A
GROUPOID : U = (A : U) * groupoid A

isEquiv (A B : U) (f : A -> B) : U = (y : B) -> isContr (fiber A B f y)

equiv (A B : U) : U = (f : A -> B) * isEquiv A B f

contrSingl (A : U) (a b : A) (p : Path A a b) :
  Path ((x : A) * Path A a x) (a,<_>a) (b,p) = <i> (p @ i,<j> p @ i/\j)

idIsEquiv (A : U) : isEquiv A A (idfun A) =
  \(a : A) -> ((a,<_>a),\(z : (x : A) * Path A a x) -> contrSingl A a z.1 z.2)

idEquiv (A : U) : equiv A A = (idfun A,idIsEquiv A)

ua (A B : U) (e : equiv A B) : Path U A B =
  <i> Glue B [ (i = 0) -> (A,e)
             , (i = 1) -> (B,idEquiv B) ]

rotLoop : (a : S1) -> Path S1 a a = split
  base1 -> <i> loop @ i
  loop1 @ i -> constSquare S1 base1 loop @ i

rot : S1 -> S1 -> S1 = split
  base1 -> \(y : S1) -> y
  loop1 @ i -> \(y : S1) -> rotLoop y @ i

isContrIdIsEquiv (A : U) : isContr (isEquiv A A (idfun A)) = (idIsEquiv A,goal)
  where
  goal (q : isEquiv A A (idfun A)) : Path (isEquiv A A (idfun A)) (idIsEquiv A) q =
    undefined

rotIsEquiv_loop_alt_unf : PathP (<i> isEquiv S1 S1 (rot (loop @ i))) (idIsEquiv S1) (idIsEquiv S1) = undefined
  -- <i> \(y : S1) -> hcomp (isEquiv S1 S1 (rot (loop @ i)))
  --           (transGen (<j> isEquiv S1 S1 (rot (loop @ i /\ j))) (-i) (idIsEquiv S1))
  --             [ (i=0) -> <_> idIsEquiv S1
  --             , (i=1) -> <i> (isContrIdIsEquiv S1).2 (transGen (<i> isEquiv S1 S1 (rot (loop @ i))) 0 (idIsEquiv S1)) @ -i] y

rotIsEquiv : (a : S1) -> isEquiv S1 S1 (rot a) = split
   base1 -> idIsEquiv S1
   loop1 @ i -> rotIsEquiv_loop_alt_unf @ i

inv : S1 -> S1 = split
  base1 -> base1
  loop1 @ i -> loop @ -i

rotrotinvloop1 (a : S1) : PathP (<i> Path S1 (rotLoop (rotLoop a @ - i) @ i) a) (<_> a) (<_> a) =
  <i j> hcomp S1 (rotLoop (rotLoop a @ -i \/ j) @ i)
                 [ (i = 0) -> <_> a
                 , (i = 1) -> <k> rotLoop a @ j /\ -k
                 , (j = 0) -> <_> rotLoop (rotLoop a @ - i) @ i
                 , (j = 1) -> <k> rotLoop a @ i /\ -k ]

square : Path (Path U S1 S1) (<_> S1) (<_> S1) =
  <i j> Glue S1 [ (i = 0) -> (S1, idEquiv S1)
                , (i = 1) -> (S1, idEquiv S1)
                , (j = 0) -> (S1, (rot (loop @ i), rotIsEquiv (loop @ i)))
                , (j = 1) -> (S1, idEquiv S1)
                ]

Hopf : S2 -> U = split
  base2 -> S1
  loop2 @ i j -> square @ i @ j

data PostTotalHopf =
    b (x : S1)
  | l (x : S1) <i j> [ (i=0) -> b x
                     , (i=1) -> b x
                     , (j=0) -> b (rot (loop @ -i) x)
                     , (j=1) -> b x ]

tee12 : (x : S2) -> Hopf x -> PostTotalHopf = split
  base2 -> (\ (y : S1) -> b y)
  loop2 @ i j -> (\ (y : Hopf (loop2{S2} @ i @ j)) ->
    hcomp PostTotalHopf
      (l{PostTotalHopf}
        (unglue y S1
          [ (i = 0) -> (S1, idEquiv S1)
          , (i = 1) -> (S1, idEquiv S1)
          , (j = 0) -> (S1, (rot (loop @ i), rotIsEquiv (loop @ i)))
          , (j = 1) -> (S1, idEquiv S1)
          ])
        @ i @ j)
      [ (i=0) -> <_> b y
      , (i=1) -> <_> b y
      , (j=0) -> <k> b (rotrotinvloop1 y @ -i @ k)
      , (j=1) -> <_> b y
      ])

tee34 : PostTotalHopf -> join S1 S1 = split
  b x -> inl x
  l x @ i j ->
    hcomp (join S1 S1) (push{join S1 S1} (rot (loop @ -i) x) x @ j)
      [ (i=0) -> <k> push {join S1 S1} x x @ (j /\ -k)
      , (i=1) -> <k> push {join S1 S1} x x @ (j /\ -k)
      , (j=0) -> <_> inl (rot (loop @ -i) x)
      , (j=1) -> <k> push {join S1 S1} x x @ -k
      ]

tee : (x : S2) -> (Hopf x -> join S1 S1) = \(x : S2) (y : Hopf x) -> tee34 (tee12 x y)

fibOmega (B : ptType) (P : B.1 -> U) (f : P (pt B)) (p : (Omega B).1) : U =
  PathP (<i> P (p @ i)) f f
itFibOmega2 (B : ptType) (P : B.1 -> U) (f : P (pt B)) : (Omega2 B).1 -> U =
  fibOmega (Omega B) (fibOmega B P f) (<_> f)
itFibOmega3 (B : ptType) (P : B.1 -> U) (f : P (pt B)) : (Omega3 B).1 -> U =
  itFibOmega2 (Omega B) (fibOmega B P f) (<_> f)

-- B.8.3 Looping the Hopf fibration

HopfThree : (Omega3 S2pt).1 -> U = itFibOmega3 S2pt Hopf base1

ZSet : set Z = undefined

predZ : Z -> Z = split
  pos u -> auxpredZ u
    where
    auxpredZ : nat -> Z = split
      zero  -> neg zero
      suc n -> pos n
  neg v -> neg (suc v)

sucZ : Z -> Z = split
  pos u -> pos (suc u)
  neg v -> auxsucZ v
    where
    auxsucZ : nat -> Z = split
      zero  -> pos zero
      suc n -> neg n

sucpredZ : (x : Z) -> Path Z (sucZ (predZ x)) x = undefined

ctr (y : Z) : fiber Z Z sucZ y = (predZ y,<i> sucpredZ y @ -i)

contr (y : Z) (y' : fiber Z Z sucZ y) : Path ((x : Z) * Path Z y (sucZ x)) (ctr y) y' = undefined

equivSucZ : isEquiv Z Z sucZ = \(y : Z) -> (ctr y,contr y)

sucPathZ : Path U Z Z = ua Z Z (sucZ,equivSucZ)

-- Covering space of the circle
helix : S1 -> U = split
  base1 -> Z
  loop1 @ i -> sucPathZ @ i

fibContrHopfThree (p : (Omega3 S2pt).1) : HopfThree p = undefined

h (p : (Omega3 S2pt).1) : (Omega3 (joinpt S1pt S1)).1 =
  <i j k> tee (p @ i @ j @ k) (fibContrHopfThree p @ i @ j @ k)


--------------------------------------------------------------------------------
-- B.9 Going back to pi2(S2)

-- 2-Groupoid truncation of S2
data trS2 =
    g2inc (a : S2)
  | g2squashC (a b : trS2)
              (p q : Path trS2 a b)
              (r s : Path (Path trS2 a b) p q)
              (t u : Path (Path (Path trS2 a b) p q) r s)
              <i j k l>
               [ (i=0) -> t @ j @ k @ l
               , (i=1) -> u @ j @ k @ l
               , (j=0) -> r @ k @ l
               , (j=1) -> s @ k @ l
               , (k=0) -> p @ l
               , (k=1) -> q @ l
               , (l=0) -> a
               , (l=1) -> b ]

trS2pt : ptType = (trS2,g2inc base2)

trS2rec (B : U) (bG : twogroupoid B) (f : S2 -> B) : trS2 -> B = split
  g2inc a -> f a
  g2squashC a b p q r s t u @ i j k l ->
    bG (trS2rec B bG f a)
       (trS2rec B bG f b)
       (<m> trS2rec B bG f (p @ m))
       (<m> trS2rec B bG f (q @ m))
       (<m n> trS2rec B bG f (r @ m @ n))
       (<m n> trS2rec B bG f (s @ m @ n))
       (<m n o> trS2rec B bG f (t @ m @ n @ o))
       (<m n o> trS2rec B bG f (u @ m @ n @ o))
       @ i @ j @ k @ l

trS2Trunc : twogroupoid trS2 =
  \(a b : trS2) (p q : Path trS2 a b)
   (r s : Path (Path trS2 a b) p q)
   (t u : Path (Path (Path trS2 a b) p q) r s) ->
    <i j k l> g2squashC{trS2} a b p q r s t u @ i @ j @ k @ l

-- Groupoid truncation
data gTrunc (A : U) =
    ginc (a : A)
  | gsquashC (a b : gTrunc A) (p q : Path (gTrunc A) a b)
             (r s : Path (Path (gTrunc A) a b) p q) <i j k>
               [ (i=0) -> r @ j @ k
               , (i=1) -> s @ j @ k
               , (j=0) -> p @ k
               , (j=1) -> q @ k
               , (k=0) -> a
               , (k=1) -> b ]

gTruncpt (A : ptType) : ptType = (gTrunc A.1,ginc (pt A))

gTruncRec (A B : U) (bG : groupoid B) (f : A -> B) : gTrunc A -> B = split
  ginc a -> f a
  gsquashC a b p q r s @ i j k ->
    bG (gTruncRec A B bG f a)
       (gTruncRec A B bG f b)
       (<m> gTruncRec A B bG f (p @ m))
       (<m> gTruncRec A B bG f (q @ m))
       (<m n> gTruncRec A B bG f (r @ m @ n))
       (<m n> gTruncRec A B bG f (s @ m @ n)) @ i @ j @ k

gTruncGroupoid (A : U) : groupoid (gTrunc A) =
  \(a b : gTrunc A) (p q : Path (gTrunc A) a b) (r s : Path (Path (gTrunc A) a b) p q) ->
    <i j k> gsquashC{gTrunc A} a b p q r s @ i @ j @ k

-- Set truncation
data sTrunc (A : U) =
    sinc (a : A)
  | ssquashC (a b : sTrunc A) (p q : Path (sTrunc A) a b) <i j>
      [ (i=0) -> p @ j
      , (i=1) -> q @ j
      , (j=0) -> a
      , (j=1) -> b ]

sTruncpt (A : ptType) : ptType = (sTrunc A.1,sinc (pt A))

sTruncRec (A B : U) (bS : set B) (f : A -> B) : sTrunc A -> B = split
  sinc a -> f a
  ssquashC a b p q @ i j -> bS (sTruncRec A B bS f a)
                               (sTruncRec A B bS f b)
                               (<k> sTruncRec A B bS f (p @ k))
                               (<k> sTruncRec A B bS f (q @ k)) @ i @ j

setTruncSet (A : U) : set (sTrunc A) =
  \(a b : sTrunc A) (p q : Path (sTrunc A) a b) ->
    <i j> ssquashC{sTrunc A} a b p q @ i @ j

-- B.9.2  Truncated higher Hopf fibration

multTwoAux : (x : S2) -> Path (Path trS2 (g2inc x) (g2inc x)) (<_> g2inc x) (<_> g2inc x) = split
  base2       -> <i j> g2inc (loop2{S2} @ i @ j)
  loop2 @ k l -> <i j>
    -- define a filler with all correct sides but one by defn, then fix the last with the truncation
    hcomp trS2
      (step1 @ k @ i @ j)
      [ (i=0) -> <_> gl @ k @ l
      , (i=1) -> <_> gl @ k @ l
      , (j=0) -> <_> gl @ k @ l
      , (j=1) -> <_> gl @ k @ l
      , (k=0) -> <_> gl @ i @ j
      , (k=1) -> <_> gl @ i @ j
      , (l=0) -> <_> gl @ i @ j
      , (l=1) -> <m>
        trS2Trunc (g2inc base2) (g2inc base2) (<_> g2inc base2) (<_> g2inc base2) gl gl
          (<k i j> step1 @ k @ i @ j) (<_ i j> gl @ i @ j)
          @ m @ k @ i @ j
      ]
    where
    gl : Path (Path trS2 (g2inc base2) (g2inc base2)) (<_> g2inc base2) (<_> g2inc base2) =
      <m n> g2inc (loop2{S2} @ m @ n)

    -- correct boundary except at l=1
    step1 : PathP (<k> Path (Path trS2 (gl @ k @ l) (gl @ k @ l)) (<_> gl @ k @ l) (<_> gl @ k @ l)) gl gl  =
      <k i j>
      hcomp trS2
        (gl @ i @ j)
        [ (i=0) -> <m> gl @ k @ l /\ m
        , (i=1) -> <m> gl @ k @ l /\ m
        , (j=0) -> <m> gl @ k @ l /\ m
        , (j=1) -> <m> gl @ k @ l /\ m
        , (k=0) -> <_> gl @ i @ j
        , (k=1) -> <_> gl @ i @ j
        , (l=0) -> <_> gl @ i @ j
        ]

multTwoTildeAux : (t : trS2) -> Path (Path trS2 t t) (<_> t) (<_> t) = split
  g2inc x -> multTwoAux x
  g2squashC a b p q r s t u @ k l m n -> <i j>
    g2squashC{trS2}
      (multTwoTildeAux a @ i @ j) (multTwoTildeAux b @ i @ j)
      (<k> multTwoTildeAux (p @ k) @ i @ j) (<k> multTwoTildeAux (q @ k) @ i @ j)
      (<k l> multTwoTildeAux (r @ k @ l) @ i @ j) (<k l> multTwoTildeAux (s @ k @ l) @ i @ j)
      (<k l m> multTwoTildeAux (t @ k @ l @ m) @ i @ j) (<k l m> multTwoTildeAux (u @ k @ l @ m) @ i @ j)
      @ k @ l @ m @ n

multTwoEquivAux : PathP (<i> Path (equiv trS2 trS2) (idEquiv trS2) (idEquiv trS2)) (<_> idEquiv trS2) (<_> idEquiv trS2) =
  <i j>
  ( (\(t : trS2) -> multTwoTildeAux t @ i @ j)
  , hcomp
      (isEquiv trS2 trS2 (\(t : trS2) -> multTwoTildeAux t @ i @ j))
      (transGen (<k> isEquiv trS2 trS2 (\(t : trS2) -> multTwoTildeAux t @ i @ j /\ k)) (i \/ -i \/ -j)
        (idIsEquiv trS2))
      [ (i=0) -> <l> (isContrIdIsEquiv trS2).2 (idIsEquiv trS2) @ -l
      , (i=1) -> <l> (isContrIdIsEquiv trS2).2 (idIsEquiv trS2) @ -l
      , (j=0) -> <l> (isContrIdIsEquiv trS2).2 (idIsEquiv trS2) @ -l
      , (j=1) -> <l>
        (isContrIdIsEquiv trS2).2
          (transGen (<k> isEquiv trS2 trS2 (\(t : trS2) -> multTwoTildeAux t @ i @ k)) (i \/ -i)
             (idIsEquiv trS2))
          @ -l
      ]
  )

tHopf3 : S3 -> U = split
  base3 -> trS2
  loop3 @ i j k ->
    Glue trS2 [ (i=0) -> (trS2, idEquiv trS2)
              , (i=1) -> (trS2, idEquiv trS2)
              , (j=0) -> (trS2, idEquiv trS2)
              , (j=1) -> (trS2, idEquiv trS2)
              , (k=0) -> (trS2, multTwoEquivAux @ i @ j)
              , (k=1) -> (trS2, idEquiv trS2)]

pi3S3 (p : (Omega3 S3pt).1) : (Omega2 trS2pt).1 =
  <i j> transGen (<k> tHopf3 (p @ j @ k @ i)) 0 (g2inc base2)



twogroupoidGROUPOID : twogroupoid GROUPOID = undefined

groupoidSET : groupoid SET = undefined



--------------------------------------------------------------------------------
-- B.11 Down one more dimension

-- experiment with alternative definitions in place of f8-11. seems much faster.

codeS2 (s : S2) : GROUPOID = (gTrunc (Hopf s), gTruncGroupoid (Hopf s))

codeTruncS2 : trS2 -> GROUPOID = trS2rec GROUPOID twogroupoidGROUPOID codeS2

encodeTruncS2 (p : (Omega trS2pt).1) : gTrunc S1 = transGen (<i> (codeTruncS2 (p @ i)).1) 0 (ginc base1)

codeS1 (s : S1) : SET = (sTrunc (helix s), setTruncSet (helix s))

codeTruncS1 : gTrunc S1 -> SET = gTruncRec S1 SET groupoidSET codeS1

encodeS1 (p : (Omega (gTruncpt S1pt)).1) : sTrunc Z = transGen (<i> (codeTruncS1 (p @ i)).1) 0 (sinc (pos zero))

-- g8-10 replaces f8-11

g8 : (Omega2 trS2pt).1 -> (Omega (gTruncpt S1pt)).1 =
  mapOmegaRefl (Omega trS2pt) (gTrunc S1) encodeTruncS2

g9 : (Omega (gTruncpt S1pt)).1 -> sTrunc Z = encodeS1

g10 : sTrunc Z -> Z = sTruncRec Z Z ZSet (idfun Z)

-- end of experiment

--- tests

test0To2 : (Omega3 S3pt).1 = <i j k> loop3{S3} @ i @ j @ k

f3 : (Omega3 S3pt).1 -> (Omega3 (joinpt S1pt S1)).1 =
  mapOmegaRefl3 S3pt (join S1 S1) e

test0To3 : (Omega3 (joinpt S1pt S1)).1 = f3 test0To2

f4 : (Omega3 (joinpt S1pt S1)).1 -> (Omega3 S2pt).1 =
  mapOmegaRefl3 (joinpt S1pt S1) S2 alpha

test0To4 : (Omega3 S2pt).1 = f4 test0To3

-- Rotating test0To4 seems to make both fibContrHopfThree compute a
-- lot faster and with smaller result.

test0To4_rotijk : (Omega3 S2pt).1 = <i j k> test0To4 @ i @ j @ k
test0To4_rotikj : (Omega3 S2pt).1 = <i j k> test0To4 @ i @ k @ j
test0To4_rotjik : (Omega3 S2pt).1 = <i j k> test0To4 @ j @ i @ k
test0To4_rotjki : (Omega3 S2pt).1 = <i j k> test0To4 @ j @ k @ i
test0To4_rotkij : (Omega3 S2pt).1 = <i j k> test0To4 @ k @ i @ j
test0To4_rotkji : (Omega3 S2pt).1 = <i j k> test0To4 @ k @ j @ i

{- Benchmarks on Anders' Thinkpad (8GB Ram, Core i5 2.2GHz)

> :n fibContrHopfThree test0To4_rotijk
(OUT OF MEMORY)

> :n fibContrHopfThree test0To4_rotikj
#hcomps: 545101
Time: 0m48.477s

> :n fibContrHopfThree test0To4_rotjik
(OUT OF MEMORY)

> :n fibContrHopfThree test0To4_rotjki
#hcomps: 438692
Time: 0m48.684s

> :n fibContrHopfThree test0To4_rotkij
(OUT OF MEMORY)

> :n fibContrHopfThree test0To4_rotkji
(OUT OF MEMORY)

Conclusion: rotjki is the still the best!

-}

f5 : (Omega3 S2pt).1 -> (Omega3 (joinpt S1pt S1)).1 = h

test0To5 : (Omega3 (joinpt S1pt S1)).1 = f5 test0To4

f6 : (Omega3 (joinpt S1pt S1)).1 -> (Omega3 S3pt).1 =
  mapOmegaRefl3 (joinpt S1pt S1) S3 eInv

test0To6 : (Omega3 S3pt).1 = f6 test0To5

f7 : (Omega3 S3pt).1 -> (Omega2 trS2pt).1 = pi3S3

test0To7 : (Omega2 trS2pt).1 = f7 test0To6

foo : (Omega3 S2pt).1 = <i j k> hcomp S2 base2 [ (i=0) -> <l> loop2{S2} @ l @ l
                                               , (i=1) -> <l> loop2{S2} @ l @ l
                                               , (j=0) -> <l> loop2{S2} @ l @ l
                                               , (j=1) -> <l> loop2{S2} @ l @ l
                                               , (k=0) -> <l> loop2{S2} @ l @ l
                                               , (k=1) -> <l> loop2{S2} @ l @ l
                                               ]

test633 : Z = g10 (g9 (g8 (f7 (f6 (f3 (f6 (f3 (f6 (f3 (f6 (f3 test0To2)))))))))))

goo (p : (Omega3 S2pt).1) : Z = g10 (g9 (g8 (f7 (f6 (f5 p)))))

sorghum : (Omega3 S2pt).1 =
  <i0 i1 i2>
  hcomp S2
    (hcomp S2
      (loop2{S2} @ i2 @ i0)
      [ (i0=0) -> <_> base2
      , (i0=1) -> <i3> loop2{S2} @ i1 @ i3
      , (i1=0) -> <_> loop2{S2} @ i2 @ i0
      , (i1=1) -> <_> loop2{S2} @ i2 @ i0
      , (i2=0) -> <i3> loop2{S2} @ i1 @ (i0 /\ i3)
      , (i2=1) -> <i3> loop2{S2} @ i1 @ (i0 /\ i3)
      ])
    [ (i0=0) -> <i4> loop2{S2} @ i1 @ i4
    , (i0=1) -> <i4> loop2{S2} @ i2 @ -i4
    , (i1=0) -> <i4> loop2{S2} @ i2 @ (i0 /\ -i4)
    , (i1=1) -> <i4> loop2{S2} @ i2 @ (i0 /\ -i4)
    , (i2=0) -> <i4> loop2{S2} @ i1 @ (i0 \/ i4)
    , (i2=1) -> <i4> loop2{S2} @ i1 @ (i0 \/ i4)
    ]

blud : (Omega2 trS2pt).1 = f7 (f6 (f5 sorghum))

-- Unfolded test0To4
gum : (Omega3 S2pt).1 =
  <i0 i1 i2>
  hcomp S2
    (hcomp S2
      (loop2{S2} @ i2 @ i0)
      [ (i0=0) -> <_> base2
      , (i0=1) -> <i3> loop2{S2} @ i1 @ i3
      ])
    [ (i0=0) -> <i4>
      hcomp S2
        (hcomp S2
          base2
          [ (i4=1) -> <_> base2
          , (i4=0) -> <_> base2
          ])
        [ (i1=0) -> <i6>
          hcomp S2
            base2
            [ (i6=1) -> <_> base2
            , (i4=1) -> <_> base2
            , (i4=0)(i6=0) -> <_> base2
            ]
        , (i1=1) -> <i6>
          hcomp S2
            base2
            [ (i6=1) -> <_> base2
            , (i4=1) -> <_> base2
            , (i4=0)(i6=0) -> <_> base2
            ]
        , (i4=0) -> <i6>
          hcomp S2
            base2
            [ (i6=1) -> <_> base2
            , (i6=0) -> <i8> loop2{S2} @ i1 @ i8
            ]
        , (i4=1) -> <_> base2
        ]
    , (i0=1) -> <i4>
      hcomp S2
        (loop2{S2} @ i2 @ -i4)
        [ (i4=1) -> <_> base2
        , (i4=0) -> <_> base2
        ]
    , (i1=0) -> <i4>
      hcomp S2
        (loop2{S2} @ i2 @ (i0 /\ -i4))
        [ (i0=0) -> <_> base2
        , (i4=1) -> <_> base2
        , (i0=1)(i4=0) -> <_> base2
        ]
    , (i1=1) -> <i4>
      hcomp S2
        (loop2{S2} @ i2 @ (i0 /\ -i4))
        [ (i0=0) -> <_> base2
        , (i4=1) -> <_> base2
        , (i0=1)(i4=0) -> <_> base2
        ]
    , (i2=0) -> <i4>
      hcomp S2
        (hcomp S2
          base2
          [ (i4=1) -> <_> base2
          , (i4=0) -> <_> base2
          ])
        [ (i0=1) -> <i6>
          hcomp S2
            base2
            [ (i4=1) -> <_> base2
            , (i4=0) -> <_> base2
            ]
        , (i1=0) -> <i6>
          hcomp S2
            base2
            [ (i0=0)(i6=1) -> <_> base2
            , (i4=1) -> <_> base2
            , (i0=1)(i4=0) -> <_> base2
            , (i4=0)(i6=0) -> <_> base2]
        , (i1=1) -> <i6>
          hcomp S2
            base2
            [ (i0=0)(i6=1) -> <_> base2
            , (i4=1) -> <_> base2
            , (i0=1)(i4=0) -> <_> base2
            , (i4=0)(i6=0) -> <_> base2
            ]
        , (i4=0) -> <i6>
          hcomp S2
            base2
            [ (i0=0)(i6=1) -> <_> base2
            , (i0=1) -> <i8> loop2{S2} @ i1 @ i8
            , (i6=0) -> <i8> loop2{S2} @ i1 @ i8
            ]
        , (i4=1) -> <_> base2
        ]
    , (i2=1) -> <i4>
      hcomp S2
        (hcomp S2
          base2
          [ (i4=1) -> <_> base2
          , (i4=0) -> <_> base2
          ])
        [ (i0=1) -> <i6>
          hcomp S2
            base2
            [ (i4=1) -> <_> base2
            , (i4=0) -> <_> base2
            ]
        , (i1=0) -> <i6>
          hcomp S2
            base2
            [ (i0=0)(i6=1) -> <_> base2
            , (i4=1) -> <_> base2
            , (i0=1)(i4=0) -> <_> base2
            , (i4=0)(i6=0) -> <_> base2
            ]
        , (i1=1) -> <i6>
          hcomp S2
            base2
            [ (i0=0)(i6=1) -> <_> base2
            , (i4=1) -> <_> base2
            , (i0=1)(i4=0) -> <_> base2
            , (i4=0)(i6=0) -> <_> base2
            ]
        , (i4=0) -> <i6>
          hcomp S2 base2
            [ (i0=0)(i6=1) -> <_> base2
            , (i0=1) -> <i8> loop2{S2} @ i1 @ i8
            , (i6=0) -> <i8> loop2{S2} @ i1 @ i8
            ]
        , (i4=1) -> <_> base2
        ]
   ]

ntest0To4_rotjki : (Omega3 S2pt).1 =
 <i j k> hcomp S2 (hcomp S2 (loop2{S2} @ i @ j)
                           [ (j=0) -> <_> base2
                           , (j=1) -> <l> loop2{S2} @ k @ l ])
                 [ (j=0) -> <l> hcomp S2 (hcomp S2 base2 [ (l=1) -> <_> base2
                                                         , (l=0) -> <_> base2])
                                         [ (k=0) -> <m> hcomp S2 base2 [ (m=1) -> <_> base2
                                                                       , (l=1) -> <_> base2
                                                                       , (l=0)(m=0) -> <_> base2 ]
                                         , (k=1) -> <m> hcomp S2 base2 [ (m=1) -> <_> base2
                                                                       , (l=1) -> <_> base2
                                                                       , (l=0)(m=0) -> <_> base2 ]
                                         , (l=0) -> <m> hcomp S2 base2 [ (m=1) -> <_> base2
                                                                       , (m=0) -> <n> loop2{S2} @ k @ n ]
                                             , (l=1) -> <_> base2 ]
                 , (j=1) -> <l> hcomp S2 (loop2{S2} @ i @ -l)
                                         [ (l=1) -> <_> base2
                                         , (l=0) -> <_> base2 ]
                 , (k=0) -> <l> hcomp S2 (loop2{S2} @ i @ j /\ -l)
                                         [ (j=0) -> <_> base2
                                         , (l=1) -> <_> base2
                                         , (j=1)(l=0) -> <_> base2 ]
                 , (k=1) -> <l> hcomp S2 (loop2{S2} @ i @ j /\ -l)
                                         [ (j=0) -> <_> base2
                                         , (l=1) -> <_> base2
                                         , (j=1)(l=0) -> <_> base2 ]
                 , (i=0) -> <l> hcomp S2 (hcomp S2 base2 [ (l=1) -> <_> base2
                                                         , (l=0) -> <_> base2 ])
                                         [ (j=1) -> <m> hcomp S2 base2 [ (l=1) -> <_> base2
                                                                       , (l=0) -> <_> base2]
                                         , (k=0) -> <m> hcomp S2 base2 [ (j=0)(m=1) -> <_> base2
                                                                       , (l=1) -> <_> base2
                                                                       , (j=1)(l=0) -> <_> base2
                                                                       , (l=0)(m=0) -> <_> base2]
                                         , (k=1) -> <m> hcomp S2 base2 [ (j=0)(m=1) -> <_> base2
                                                                       , (l=1) -> <_> base2
                                                                       , (j=1)(l=0) -> <_> base2
                                                                       , (l=0)(m=0) -> <_> base2]
                                         , (l=0) -> <m> hcomp S2 base2 [ (j=0)(m=1) -> <_> base2
                                                                       , (j=1) -> <n> loop2{S2} @ k @ n
                                                                       , (m=0) -> <n> loop2{S2} @ k @ n ]
                                         , (l=1) -> <_> base2 ]
                 , (i=1) -> <l> hcomp S2 (hcomp S2 base2 [ (l=1) -> <_> base2
                                                         , (l=0) -> <_> base2])
                                         [ (j=1) -> <_> hcomp S2 base2 [ (l=1) -> <_> base2
                                                                       , (l=0) -> <_> base2]
                                         , (k=0) -> <m> hcomp S2 base2 [ (j=0)(m=1) -> <_> base2
                                                                       , (l=1) -> <_> base2
                                                                       , (j=1)(l=0) -> <_> base2
                                                                       , (l=0)(m=0) -> <_> base2]
                                         , (k=1) -> <m> hcomp S2 base2 [ (j=0)(m=1) -> <_> base2
                                                                       , (l=1) -> <_> base2
                                                                       , (j=1)(l=0) -> <_> base2
                                                                       , (l=0)(m=0) -> <_> base2]
                                         , (l=0) -> <m> hcomp S2 base2 [ (j=0)(m=1) -> <_> base2
                                                                       , (j=1) -> <n> loop2{S2} @ k @ n
                                                                       , (m=0) -> <n> loop2{S2} @ k @ n ]
                                        , (l=1) -> <m> base2 ] ]

-- test63n = loop f6-f3 n times
test631 : Z = g10 (g9 (g8 (f7 (f6 (f3 test0To2)))))
test632 : Z = g10 (g9 (g8 (f7 (f6 (f3 (f6 (f3 test0To2)))))))
test633 : Z = g10 (g9 (g8 (f7 (f6 (f3 (f6 (f3 (f6 (f3 test0To2)))))))))
test634 : Z = g10 (g9 (g8 (f7 (f6 (f3 (f6 (f3 (f6 (f3 (f6 (f3 test0To2)))))))))))
test635 : Z = g10 (g9 (g8 (f7 (f6 (f3 (f6 (f3 (f6 (f3 (f6 (f3 (f6 (f3 test0To2)))))))))))))
test636 : Z = g10 (g9 (g8 (f7 (f6 (f3 (f6 (f3 (f6 (f3 (f6 (f3 (f6 (f3 (f6 (f3 test0To2)))))))))))))))
test637 : Z = g10 (g9 (g8 (f7 (f6 (f3 (f6 (f3 (f6 (f3 (f6 (f3 (f6 (f3 (f6 (f3 (f6 (f3 test0To2)))))))))))))))))
test638 : Z = g10 (g9 (g8 (f7 (f6 (f3 (f6 (f3 (f6 (f3 (f6 (f3 (f6 (f3 (f6 (f3 (f6 (f3 (f6 (f3 test0To2)))))))))))))))))))
test639 : Z = g10 (g9 (g8 (f7 (f6 (f3 (f6 (f3 (f6 (f3 (f6 (f3 (f6 (f3 (f6 (f3 (f6 (f3 (f6 (f3 (f6 (f3 test0To2)))))))))))))))))))))


-- Definition of Brunerie
brunerie : Z = g10 (g9 (g8 test0To7))

-- Optimized version using the optimal rotation of test0To4 (?)
brunerie_opt : Z = g10 (g9 (g8 (f7 (f6 (f5 ntest0To4_rotjki)))))


opaque twogroupoidGROUPOID
opaque groupoidSET
opaque fibContrHopfThree
